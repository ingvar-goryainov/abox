variable "cluster_name" {
  description = "Cluster Name"
  type        = string
  default     = "abox"
}

variable "node_image" {
  description = "KinD node image. Ceiling is the kind CLI version installed by scripts/setup.sh."
  type        = string
  default     = "kindest/node:v1.37.0"
}

variable "kubeconfig_path" {
  description = "Kubeconfig written by kind and read by the helm/kubernetes/kubectl providers."
  type        = string
  default     = "~/.kube/config"
}

variable "oci_registry" {
  description = "OCI registry base URL"
  type        = string
  default     = "oci://ghcr.io/den-vasyliev/abox"
}

variable "releases_artifact" {
  description = "OCI repository holding the releases artifact, under var.oci_registry"
  type        = string
  # main publishes to "releases". Every v* tag cut from a feature branch would
  # land in that same stream -- the RSIP filter is ^\d+\.\d+\.\d+$ with
  # limit 1, so the newest tag from any branch would win and a cluster
  # bootstrapped from main would get this branch's bundle. feat/llmd-embeddings
  # therefore has its own repository, matching the name
  # .github/workflows/flux-push.yaml derives from the branch.
  default = "releases-llmd-embeddings"
}

variable "releases_version" {
  description = "Default tag for releases OCI artifact bootstrap"
  type        = string
  default     = "0.1.0"
}

variable "flux_operator_version" {
  description = "flux-operator Helm chart version. Unset in the module defaults, which floats to latest."
  type        = string
  default     = "0.59.0"
}

variable "bootstrap_revision" {
  description = "Bump to force the flux-operator bootstrap Job to re-run without an input change"
  type        = number
  default     = 1
}

variable "google_api_key" {
  description = "AI Studio key for releases/model-configs.yaml, written to Secret kagent-gemini. Set TF_VAR_google_api_key, which scripts/setup.sh loads from .env. Left empty the Secret is skipped and the agents will not start."
  type        = string
  sensitive   = true
  default     = ""
}

variable "google_api_key_revision" {
  description = "Bump to push a rotated google_api_key. The Secret's value is a write-only attribute, so OpenTofu cannot read it back to notice it changed."
  type        = number
  default     = 1
}
